'++LotusScript Development Environment:2:5:(Options):0:74
Option Public
Option Declare

Use "cls.system"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Function insertPhoto( sourceDocument As NotesDocument, sourceObject As Variant )
Declare Function insertResponses( sourceDocument As NotesDocument, sourceObject As Variant )
Declare Function getData( sourceDocument As NotesDocument, sourceObject As Variant ) As String

'++LotusScript Development Environment:2:5:(Declarations):0:2

'++LotusScript Development Environment:2:1:insertPhoto:1:8
Function insertPhoto( sourceDocument As NotesDocument, sourceObject As Variant )
	Dim photo As NotesRichTextItem
	Set photo = SourceDocument.getFirstItem( "Body" )
	If photo Is Nothing Then Exit Function
	
	Dim path As String
	path = Environ( "Temp" ) 
	Dim filename As String
	
	'extract file
	If Not Isarray( photo.EmbeddedObjects  ) Then Exit Function
	
	Forall e In photo.EmbeddedObjects 
		filename = path & "\" & e.Source
		Call e.ExtractFile( filename )
	End Forall
	
	'insert in word
	Dim w As Variant
	Set w = sourceObject.ActiveDocument
	Dim Selection As Variant
	Set Selection = w.ActiveWindow.Selection
	
	Const wdGoToBookmark = -1
	Const wdCharacter = 1
	Const wdExtend = 1
	Const wdSortByName = 0
	Const wdFindContinue = 1
	
	Call Selection.goto( wdGoToBookmark,,,"photo"  )
	Call Selection.InlineShapes.AddPicture( filename, False, True )
	
	'cleanup
	Kill filename
	
End Function
'++LotusScript Development Environment:2:1:insertResponses:1:8
Function insertResponses( sourceDocument As NotesDocument, sourceObject As Variant )
	On Error Goto eh
	
'collect the data and calculate the html
	Dim html As String
	
	Dim coll As NotesDocumentCollection
	Dim resp As NotesDocument
	Set coll = sourceDocument.Responses
	If coll Is Nothing Then Exit Function
	
	html = |<table>|
	Set resp = coll.GetFirstDocument
	Do Until resp Is Nothing
		html = html & |<tr><td>| & resp.subject( 0 ) & |</td><td>| & resp.Created & |</td><td>| & resp.Authors( 0 ) & |</td></tr>|
		
		Set resp = coll.GetNextDocument( resp )
	Loop
	html = html & |</table>|
	
	
'save html to file
	Dim path As String
	path = Environ( "Temp" ) 
	Dim filename As String
	filename = path & "\responses.html"
	
	Dim f As Integer
	f = Freefile
	Open filename For Output As #f
	Print #f, html
	Close f
	
'insert in word
	Dim w As Variant
	Set w = sourceObject.ActiveDocument
	Dim Selection As Variant
	Set Selection = w.ActiveWindow.Selection
	
	Const wdGoToBookmark = -1
	
	Call Selection.goto( wdGoToBookmark,,,"responses"  )
	Call Selection.InsertFile( filename )
	
	
'clear the file
	Kill filename
	
	Exit Function
eh:
	Print getErrorInfo( "" )
	Error Err, getErrorInfo( "" )
End Function
'++LotusScript Development Environment:2:1:getData:1:8
Function getData( sourceDocument As NotesDocument, sourceObject As Variant ) As String
	'This sample functions, just returns some misc. Notes info
	Dim session As New NotesSession
	Dim db As NotesDatabase
	Set db = session.CurrentDatabase
	
	Dim s As String
	s = "Platform: " & session.Platform
	s = s & ", Version: " & session.NotesVersion
	s = s & ", File format: " & db.FileFormat
	
	getData = s
End Function