'++LotusScript Development Environment:2:5:(Options):0:74
Option Public
Option Declare
Use "cls.system"

%REM @revisions
	Rev 6.9.3 majkilde 22.03.2012
	- Added: getNumberEx
%END REM


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class number As AbstractObject

'++LotusScript Development Environment:2:5:(Declarations):0:10
Public Class number As AbstractObject
'###########################################################################################
	Private db As NotesDatabase
	Private view As NotesView
	
	Sub new()
	'--------------------------------------------------------------------------------------------------------------------------------------------
		Set db = session.CurrentDatabase
	End Sub
	
%REM
Creates a number document for each server in the domain
%END REM
	Public Function initAll( dbid As Variant )
	'--------------------------------------------------------------------------------------------------------------------------------------------
		Dim nab As New NotesDatabase( db.Server, "names.nsf" )
		Dim nabView As NotesView
		Dim serverDoc As NotesDocument
		
		Set nabView = nab.GetView( "($Servers)" )
		Set serverDoc = nabView.GetFirstDocument()
		Do Until serverDoc Is Nothing
			Call getNumberDocument( dbid, serverDoc.serverName( 0 ))
			
			Set serverDoc = nabView.GetNextDocument( serverDoc )
		Loop
	End Function
	
	%REM
	
%END REM
	Private Function getNumberDocument( dbid As Variant, key As String ) As NotesDocument
		On Error GoTo eh
		
		Set db = CacheManager.getDatabase( dbid )
		Set view = CacheManager.GetView( db, "($Numbers)" )
		
		Dim doc As NotesDocument
		Set doc = view.GetDocumentByKey( key )
		
		If doc Is Nothing Then
			Set doc = db.CreateDocument
			doc.form = "Number"
			Call doc.ReplaceItemValue( "key", key )
			doc.serie = view.AllEntries.Count + 1
			doc.number = 0
			
			Call doc.ReplaceItemValue( "$ConflictAction", "2" )
			Call doc.ReplaceItemValue( "$PublicAccess", "1" )
		End If
		
		Set getNumberDocument = doc
		
done: 
		Exit Function
eh: 
		Error Err, getErrorInfo( Me )
	End Function
	
	%REM
		Function getNumberEx
		dbid: get the number from this database.
			- NotesDatabase
			- "*" Current database
			- "<DbConfig Alias>"
			- "server!!filename", where server could be *, home or just the name of the server
		note: always get the number from the same server to ensure uniqueness
		
		numberSerie - name of the serie
	%END REM
	Public Function getNumberEx( dbid As Variant, numberSerie As string ) As integer
		On Error GoTo eh
		Dim db As NotesDatabase
		Set db = CacheManager.getDatabase( dbid )

		Dim doc As NotesDocument
RETRY:		
		Set doc = getNumberDocument( db, numberSerie )
		
		'assign a new number
		doc.number = doc.number( 0 )+1
		If Not doc.Save( True, False ) Then 
			Set doc = Nothing
			GoTo RETRY
		End If
		
		'return
		getNumberEx = doc.number( 0 )

done: 
		Exit function
eh: 
		Error Err, getErrorInfo( Me )
	End Function
	
	%REM
		
	%END REM
	Public Function getNumber() As String
		Dim doc As NotesDocument
		Dim key As String
		
		If db.Server = "" Then
			key = session.UserName
		Else
			key = db.Server
		End If
		
RETRY:		
		Set doc = getNumberDocument( "*", key )
		
		'assign a new number
		doc.number = doc.number( 0 )+1
		If Not doc.Save( True, False ) Then 
			Set doc = Nothing
			Goto RETRY
		End If
		
		
		'return
		getNumber = doc.serie( 0 ) & "-" & Right( "000000" & Cstr( doc.number( 0 )), 5 )
		
	End Function
End Class